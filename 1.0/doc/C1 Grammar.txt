<program> ::= <block>

<stmt_list> ::= <stmt_list> <stmt>
              | ε

<stmt> ::= <block>
         | <assign_stmt>
         | <expr_stmt>
         | <if_stmt>
         | <while_stmt>
         | <break_stmt> 
         | <continue_stmt>
         | <input_stmt>
         | <output_stmt>

<block> ::= "{" <stmt_list> "}"

<assign_stmt> ::= <access> "=" <expr> ";"

<expr_stmt> ::= <expr> ";"

<if_stmt> ::= "if" "(" <expr> ")" <stmt> 
            | "if" "(" <expr> ")" <stmt> "else" <stmt>

<while_stmt> ::= "while" "(" <expr> ")" <stmt>

<break_stmt> ::= "break" ";"

<continue_stmt> ::= "continue" ";"

<input_stmt> ::= "input" "(" <access> ")" ";"

<output_stmt> ::= "output" "(" <expr> ")" ";"

<expr> ::= <expr> <bi_op_1> <expr_1> | <expr_1>

<expr_1> ::= <expr_1> <bi_op_2> <expr_2> | <expr_2>

<expr_2> ::= <expr_2> <bi_op_3> <expr_3> | <expr_3>

<expr_3> ::= <expr_3> <bi_op_4> <expr_4> | <expr_4>

<expr_4> ::= <expr_4> <bi_op_5> <expr_5> | <expr_5>

<expr_5> ::= <expr_5> <bi_op_6> <expr_6> | <expr_6>

<expr_6> ::= <un_op> <expr7> | <expr_7>

<expr_7> ::= "(" <expr> ")" | <access> | <literal>

<un_op> ::= "!" | "+" | "-"

<bi_op_1> ::= "||"

<bi_op_2> ::= "&&"

<bi_op_3> ::= "==" | "!="

<bi_op_4> ::= "<" | "<=" | ">" | ">="

<bi_op_5> ::= "+" | "-"

<bi_op_6> ::= "*"

<access> ::= id

<literal> ::= integer_literal